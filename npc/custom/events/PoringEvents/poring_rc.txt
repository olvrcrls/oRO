function	script	GetNumSuffix	{
	set .@n, getarg(0);
	set .@mod, .@n % 10;
	if      (.@mod == 1 && .@n != 11) return .@n+"st";
	else if (.@mod == 2 && .@n != 12) return .@n+"nd";
	else if (.@mod == 3 && .@n != 13) return .@n+"rd";
	else return .@n+"th";
}


kamizama,182,168,5	script	Race Ranking::poring_rc_rank	875,{
	doevent "poring_rc_main::OnRank";
}

//prontera,155,172,5	duplicate(poring_rc_rank)	Race Ranking#1	875
// prontera,155,173,5	duplicate(poring_rc_rank)	Race Ranking#2	875
// prontera,155,174,5	duplicate(poring_rc_rank)	Race Ranking#3	875
// prontera,155,175,5	duplicate(poring_rc_rank)	Race Ranking#4	875
// prontera,155,176,5	duplicate(poring_rc_rank)	Race Ranking#5	875
// prontera,155,177,5	duplicate(poring_rc_rank)	Race Ranking#6	875
// prontera,155,178,5	duplicate(poring_rc_rank)	Race Ranking#7	875


kamizama,152,156,5	script	poring_rc_portal	45,2,2,{
	OnTouch:
		doevent "poring_rc_main::OnEnter";
		end;
}

poring_rc,68,111,4	script	Poring Race Track	875,{
	emotion et_go;
	end;
}

-	script	poring_rc_main	-1,{
	end;
	
	OnFinishRound:
		if ( .status == 2 ) {
		if (!ismounting()) {
			dispbottom "You didn't follow the rules";
			warp "SavePoint",0,0;
			} else {
			@checkpoint = 0;
			@round_count++;
			if ( @round_count >= .round ) {
				.total_winner_count++;
				switch ( .total_winner_count ) {
					case 1: 
						getitem 7539,25;
						break;
					case 2:
						getitem 7539,10;
						break;
					case 3:
						getitem 7539,5;
						break;
					default:
						getitem 7539,1;
						break;
				}
				.@name$ = strcharinfo(0);
				.@second = getnpctimer(0);
				query_sql( "INSERT INTO `event_poringrace_rank` ( `aid`,`cid`,`name`,`record`,`time` ) VALUES ( "+getcharid(3)+","+getcharid(0)+",'"+escape_sql( .@name$ )+"',"+.@second+",NOW() ) " );
				mes "You're the "+F_GetNumSuffix( .total_winner_count )+" players who finished the tracks.";
				mes "^FF0000( times: "+sprintf( "%d.%03d",( .@second / 1000 ),( .@second % 1000 ) )+" seconds )^000000";
				mapannounce .npc_map$,.@name$+" - "+F_GetNumSuffix( .total_winner_count )+" players finished the tracks. ( times: "+sprintf( "%d.%03d",( .@second / 1000 ),( .@second % 1000 ) )+" seconds )",bc_map;
				// query records
				close2;
				donpcevent .npc_name$+"::OnRankUpdate";
				if (ismounting()) setmounting;
				warp "SavePoint",0,0;
			}
			else {
				announce "[Poring Race] Round "+@round_count+" / "+.round+"!!",bc_self;
			}
		}
	}
		end;
	OnRank:
		mes "Race Ranking : ";
		if ( !.cid_size ) {
			mes "No records founds.";
		}
		else {
			.@cid = getcharid(0);
			for ( .@i = 0; .@i < .cid_size; .@i++ ) {
				mes F_GetNumSuffix( .@i+1 )+" - "+.name$[.@i];
				mes "Record -^777777 "+sprintf( "%d.%03d",( .record[.@i] / 1000 ), ( .record[.@i] % 1000 ) )+" sec^000000";
				mes " ";
				if ( .cid[.@i] == .@cid ) 
					.@buff++;
			}
			if ( .@buff ) 
				if ( select( "Buff Me","Cancel" ) == 1 ) {
					specialeffect2 EF_HEAL2; percentheal 100,100;
					specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
					specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
				}
		}
		close;
		
	OnInit:
		// how many round
		.round = 2;
		
		// event rules / broadcast message
		setarray .message$,
			"This is Racing Event.",
			"Every players required to use MOUNT.",
			"Penalty will be given to players who break the rules.",
			"There exist some random penalty spot, becareful.",
			"There are several checkpoints you need to pass before you can finish one round.",
			"By the way, it's a Anti-Clockwise direction racing.",
			"Enjoy and race!";
		.message_size = getarraysize( .message$ );
		
		.npc_name$ = strnpcinfo(3);
		getmapxy( .npc_map$,.npc_x,.npc_y,BL_NPC,"Poring Race Track" );
		sleep 1000;
		disablenpc "poring_rc_portal";
		mapwarp .npc_map$,.npc_map$,.npc_x,.npc_y;
		movenpc "poring_rc_checkpoint#0",.npc_x,.npc_y;
	
	OnRankUpdate:
		deletearray .cid;
		query_sql( "SELECT `cid`,`name`,`record` FROM `event_poringrace_rank` ORDER BY `record` LIMIT 10",.cid,.name$,.record );
		.cid_size = getarraysize( .cid );
		end;
		
	OnMinute30:
	OnEventStart:
		discord("Poring Race Event initiated!", "event");
		if ( !.status ) {
			.status = 1;
			.total_winner_count = 0;
			
			for ( .@i = 0; .@i <= 6; .@i++ ) {
				donpcevent "#poring_rc_wall_1"+.@i+"::OnEnable";
				donpcevent "#poring_rc_wall_2"+.@i+"::OnEnable";
			}
			enablenpc "poring_rc_portal";
			for ( .@i = 3; .@i > 0; .@i-- ) {
				announce "[Poring Race] Racing Event will start in "+.@i+" minutes. Join the Portal.",bc_all;
				sleep 60000;
			}
			disablenpc "poring_rc_portal";
			announce "[Poring Race] Racing Event has begun.",bc_all;
			
			for ( .@i = 0; .@i < .message_size; .@i++ ) {
				mapannounce .npc_map$,.message$[.@i],bc_map;
				npctalk .message$[.@i];
				sleep 5000;
			}
			
			for ( .@i = 3; .@i > 0; .@i-- ) {
				mapannounce .npc_map$,"[Poring Race] "+.@i,bc_map;
				npctalk ""+.@i;
				sleep 1000;
			}
			
			mapannounce .npc_map$,"[Poring Race] GO!!!",bc_map;
			npctalk "GO!!";
			emotion et_go;
			
			for ( .@i = 0; .@i <= 6; .@i++ ) {
				donpcevent "#poring_rc_wall_1"+.@i+"::OnDisable";
				donpcevent "#poring_rc_wall_2"+.@i+"::OnDisable";
			}
			
			.status = 2;
			initnpctimer;
		}
		end;
		
	OnTimer2700000: // 45 minutes
		.status = 0;
		stopnpctimer;
		mapannounce .npc_map$,"[Poring Race] Race Event Time's UP!",bc_map;
		sleep 5000;
		mapannounce .npc_map$,"[Poring Race] Race Event Result - We got "+.total_winner_count+" winners.",bc_map;
		sleep 10000;
		mapwarp .npc_map$,.npc_map$,.npc_x,.npc_y;
		end;
		
	OnEnter:
		if ( .status == 1 ) {
			if ( ismounting() ) {
				dispbottom "<Race Track> Please remove your mount.";
			}
			else if ( checkriding() ) {
				dispbottom "<Race Track> Please remove your mount.";
			}
			else {
				@checkpoint = 0;
				@round_count = 0;
				sc_end sc_all;
				setmounting;
				warp .npc_map$,.npc_x,.npc_y;
			}
		}
		end;
}


poring_rc,1,1,0	script	poring_rc_checkpoint#0	139,4,4,{
	OnTouch:
		.@checkpoint = atoi( strnpcinfo(2) );
		
		if ( getvariableofnpc( .status,"poring_rc_main" ) == 2 ) {
		
			if ( @checkpoint >= .@checkpoint ) {
				sc_end sc_all;
				dispbottom "You have reached this Checkpoint # "+( ( .@checkpoint != 0 )? .@checkpoint:( @round_count == 0 ) ? 0:.max_checkpoint )+" previously.";
			}
			else if ( @checkpoint != ( .@checkpoint - 1 ) ) {
				mes "You didnt reach Checkpoint # "+( ( .@checkpoint == 0 )? .max_checkpoint:( .@checkpoint-1 ) )+".";
				close2;
				// warp 
			}
			else {
				.@next = (( .@checkpoint+1 ) % .max_checkpoint );
				@checkpoint = .@next - 1;
				
				
				getmapxy( .@map$,.@x,.@y,BL_NPC,"poring_rc_checkpoint#"+.@next );
				viewpoint 1,.@x,.@y,1,0x00EE00;
				announce "You reached "+F_GetNumSuffix( .@checkpoint )+" Checkpoint. "+F_GetNumSuffix( .@next == 0 ? .max_checkpoint:.@next )+" Checkpoint at ( "+.@x+","+.@y+" )",bc_self|bc_blue;
				// announce "You reached "+F_GetNumSuffix( .@checkpoint )+" Checkpoint. ",bc_self;
				
				if ( .@next == 1 ) {
					doevent "poring_rc_main::OnFinishRound";
				}
			}
		}
		end;
		
	OnInit:
		.max_checkpoint++;
		end;
}

poring_rc,45,111,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#1	139,4,4
poring_rc,49,83,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#2	139,4,4
poring_rc,93,66,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#3	139,4,4
poring_rc,119,47,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#4	139,4,4
poring_rc,134,65,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#5	139,4,4
poring_rc,117,107,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#6	139,4,4
poring_rc,95,111,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#7	139,4,4



-	script	poring_rc_wall	1085,{
	end;
	
	OnEnable: 
		callsub( L_setup,1 );
		end;
	
	OnInit:
		if ( strnpcinfo(2) == "" ) 
			end;
	OnDisable:
		callsub( L_setup,0 );
		end;
		
	L_setup:
		.@param = getarg( 0,0 );
		.@npc_name$ = strnpcinfo(0);
		
		getmapxy( .@map$,.@x,.@y,BL_NPC );
		if ( .@map$ != "" ) {
			if ( .@param ) {
				hideoffnpc .@npc_name$;
			}
			else {
				hideonnpc .@npc_name$;
			}
			setcell .@map$,.@x,.@y,.@x,.@y,cell_walkable,!.@param;
		}
		return;
}

poring_rc,64,114,4	duplicate(poring_rc_wall)	#poring_rc_wall_10	1085
poring_rc,64,113,4	duplicate(poring_rc_wall)	#poring_rc_wall_11	1085
poring_rc,64,112,4	duplicate(poring_rc_wall)	#poring_rc_wall_12	1085
poring_rc,64,111,4	duplicate(poring_rc_wall)	#poring_rc_wall_13	1085
poring_rc,64,110,4	duplicate(poring_rc_wall)	#poring_rc_wall_14	1085
poring_rc,64,109,4	duplicate(poring_rc_wall)	#poring_rc_wall_15	1085
poring_rc,64,108,4	duplicate(poring_rc_wall)	#poring_rc_wall_16	1085

poring_rc,70,114,4	duplicate(poring_rc_wall)	#poring_rc_wall_20	1085
poring_rc,70,113,4	duplicate(poring_rc_wall)	#poring_rc_wall_21	1085
poring_rc,70,112,4	duplicate(poring_rc_wall)	#poring_rc_wall_22	1085
poring_rc,70,111,4	duplicate(poring_rc_wall)	#poring_rc_wall_23	1085
poring_rc,70,110,4	duplicate(poring_rc_wall)	#poring_rc_wall_24	1085
poring_rc,70,109,4	duplicate(poring_rc_wall)	#poring_rc_wall_25	1085
poring_rc,70,108,4	duplicate(poring_rc_wall)	#poring_rc_wall_26	1085





-	script	poring_rc_dummy	-1,{
	end;
	
	OnInit:
		if ( strnpcinfo(2) != "" ) {
			.@npc_name$ = strnpcinfo(0);
			getmapxy( .@map$,.@x,.@y,BL_NPC );
			.@sprite = rand( 669,682 );
			setnpcdisplay( .@npc_name$,.@sprite );
			while ( 1 ) {
				emotion rand( et_surprise,et_whisp );
				sleep rand( 3000,10000 );
				if ( rand( 100 ) < 10 )
					showscript callfunc("F_Rand",
						"Go ~ Go ~ Go",
						"Run ~ Run ~ Run ~",
						"Hurry!",
						"Come on!!",
						"Do your best!",
						"Faster...",
						"You gotta be kidding...",
						"You cant be this slow...",
						"Don't look back, RUN!!",
						"Pecopeco the best!",
						"Run Idol ~ run ~"
					);
			}
		}
		end;
}

poring_rc,52,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_1	1905
poring_rc,52,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_2	1905
poring_rc,52,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_3	1905
poring_rc,52,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_4	1905

poring_rc,57,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_1	1905
poring_rc,57,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_2	1905
poring_rc,57,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_3	1905
poring_rc,57,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_4	1905

poring_rc,62,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_1	1905
poring_rc,62,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_2	1905
poring_rc,62,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_3	1905
poring_rc,62,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_4	1905

poring_rc,67,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_1	1905
poring_rc,67,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_2	1905
poring_rc,67,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_3	1905
poring_rc,67,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_4	1905

poring_rc,72,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_1	1905
poring_rc,72,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_2	1905
poring_rc,72,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_3	1905
poring_rc,72,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_4	1905

poring_rc,77,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_1	1905
poring_rc,77,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_2	1905
poring_rc,77,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_3	1905
poring_rc,77,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_4	1905

poring_rc,82,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_1	1905
poring_rc,82,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_2	1905
poring_rc,82,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_3	1905
poring_rc,82,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_4	1905

poring_rc,87,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_1	1905
poring_rc,87,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_2	1905
poring_rc,87,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_3	1905
poring_rc,87,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_4	1905

poring_rc,92,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_1	1905
poring_rc,92,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_2	1905
poring_rc,92,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_3	1905
poring_rc,92,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_4	1905




-	script	poring_rc_trap	-1,{
	
	OnTouch:
		specialeffect2 EF_SKIDTRAP;
		if ( getvariableofnpc( .status,"poring_rc_main" ) == 2 ) {
			.@sc = callfunc( "F_Rand",
				SC_STONE,
				SC_FREEZE,
				SC_STUN,
				SC_SLEEP,
				SC_POISON,
				SC_CURSE,
				SC_CONFUSION,
				SC_BLIND,
				SC_DEC_AGI,
				SC_ILLUSION,
				SC_SLOWDOWN
			);
			unitstopwalk getcharid(3);
			sc_start .@sc,rand( 3000,5000 ),1;
		}
		end;
}


// adjust the coordinate of traps here.
// duplicate more if you need more.
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_1	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_2	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_3	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_4	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_5	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_6	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_7	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_8	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_9	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_10	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_11	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_12	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_13	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_14	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_15	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_16	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_17	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_18	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_19	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_20	4_SOIL,2,2

// poring_rc - No mount bug
poring_rc	mapflag	nosave
poring_rc	mapflag	nowarp
poring_rc	mapflag	nowarpto
poring_rc	mapflag	noteleport
poring_rc	mapflag	noreturn
poring_rc	mapflag	nobranch
poring_rc	mapflag	noicewall