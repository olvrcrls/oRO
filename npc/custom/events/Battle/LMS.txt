// Edit by Mabuhay

-	script	Event_LMS	-1,{
OnInit:
	// item reward
	setarray .item,
		6378, 50;

	// @lmsstart - gm manual start
	bindatcmd "lmsstart", strnpcinfo(0)+"::OnStart",99,99;
	
	// @lms - player join event
	bindatcmd "lms", strnpcinfo(0)+"::OnJoinEvent";
	
	// Event cooldown
	.event_cd = 60 * 60 * 23; // 23hr
	
	// Mapflags
	setarray .@mapflag,
		mf_nowarp,
		mf_nowarpto,
		mf_noteleport,
		mf_nomemo,
		mf_nosave,
		mf_noicewall,
		mf_nobranch,
		mf_noreturn;
		
	for ( .@i = 0; .@i < getarraysize(.@mapflag); .@i++)
		setmapflag "06guild_01", .@mapflag[.@i];
		
	.@min = gettime(DT_MINUTE)*300;
	.@sec = gettime(DT_SECOND);
	.event_time = (gettimetick(2) - ( .@min + .@sec )) + .event_cd - 5;
	end;
	
OnJoinEvent:
	mes " [ Event Status ] ";
	if ( !.start ) mes "Next schedule starts in ^FF0000"+ Time2Str(.event_time) +"^000000.";
	if ( .start == 1 ) mes "Event is currently active.";
	if ( .start == 2 ) mes "Event is currently on-going.";
	next;
	switch(select(
		"Last Man Standing Event - "+ ((.start == 1) ? "^00ff00Join Event":((.start == 2) ? "^00ff00Event On-Going":"^ff0000Not Available"))+"^000000" )) {
	case 1:
		if ( !.start ) {
			mes "Event is not available.";
			close;
		} else if ( .start == 2 ) {
			mes "Event is on-going";
			close;
		}
		else
			warp "06guild_01",0,0;
	}
	end;

OnClock0715: // runs every 2pm PHT
OnStart: .@force = true;
	.@time = gettimetick(2);
	.@min = gettime(DT_MINUTE)*300;
	.@sec = gettime(DT_SECOND);
	if ( .event_time > .@time && !.@force || .start && !.@force ) end; // doesnt start if cd isn't done.
	.event_time = (.@time - ( .@min + .@sec )) + .event_cd - 5;  
	.start = 1;
	pvpoff "06guild_01";
	discord("Last Man Standing Event initiated!", "event");
	announce "LMS Manager: We are going to have a Last Man Standing Event.",0;
	sleep 10000;
	announce "LMS Manager: For those who wants to join, use @lms",0;
	sleep 10000;
	announce "LMS Manager: After 5 Minutes Event will close.",0;
	sleep 10000;
	announce "LMS Manager: So please if you want to join. use @lms",0;
	sleep 60000;
	announce "LMS Manager: The entry to the event is now closed.",bc_all,0x0ceedb;
	if ( getmapusers("06guild_01") < 2 ) {
		announce "LMS Manager: Not enough participants.",bc_all,0x0ceedb;
		.start = false;
		end;
	}
	.start = 2;
	sleep 3500;
	mapannounce "06guild_01","We're about to begin the event.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "06guild_01","The rules are simple.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "06guild_01","Kill everyone in the map.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "06guild_01","Last Man Standing wins!",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "06guild_01","That's all. Let's begin.",bc_map,0x0ceedb;
	sleep 3000;
	for (.@i = 10; .@i > 0; .@i--) {
		mapannounce "06guild_01", "LMS Manager: -- "+.@i+" --",bc_map,0x0ceedb;
		sleep 1000;
	}
	mapannounce "06guild_01", "LMS Manager: Fight! ",bc_map,0x0ceedb;
	pvpon "06guild_01";
	.timer = 0;
	initnpctimer;
	end;
	
OnTimer5000: // check every 5 secs..
	.timer++;
	.@size = getmapusers("06guild_01");
	if ( .@size > 1 && .timer == 6 ) {
		.timer = 0;
		mapannounce "06guild_01", "LMS Manager: "+ .@size +" players left..", bc_blue;
	} 
	else if ( .@size <= 1 )
		donpcevent strnpcinfo(3)+"::OnEndEvent";
	setnpctimer 0;
	end;
	
OnEndEvent:
	stopnpctimer;
	.start = false;
	pvpoff "06guild_01";
	mapannounce "06guild_01", "LMS Manager: Event ended!",bc_map,0x0ceedb;
	sleep 5000;
	.@size = getmapunits(BL_PC,"06guild_01",.@aid);
	if ( .@size == 0 ) {
		announce "LMS Manager: No winners.",0;
		end;
	}
	.@amt = getarraysize(.item);
	attachrid .@aid;
	announce strcharinfo(0)+" has won the Last Man Standing Event.",bc_all,0x0ceedb;
	for ( .@j = 0; .@j < .@amt; .@j += 2 )
		getitem .item[.@j], .item[.@j+1];				
	warp "kamizama", 152, 141;
	end;
	
OnPCDieEvent:
	if ( .start && strcharinfo(3) == "06guild_01" ) {
		warp "kamizama", 152, 141;
		.@size = getmapusers("06guild_01");
		if ( .@size > 1 )
			mapannounce "06guild_01", "LMS Manager: "+ .@size +" players left..", bc_blue;
		else if ( .@size <= 1 )
			donpcevent strnpcinfo(3)+"::OnEndEvent";
	}
	end;
	
}