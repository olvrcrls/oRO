-	script	worldboss#yumi	-1,{

OnClock1000:
OnEventReady:
	$WBID=143;
	query_sql("TRUNCATE worldboss");
	.wave = 1;
	.MapID = rand(getarraysize(.EventMap$));
	discord("World Boss - Valfreyja Invasion initiated!", "event");
	announce "Valfreyja : Citizens of midgard! An abnormality has been detected at "+.EventMap$[.MapID]+"!",bc_all;
	sleep 10000;
	announce "Valfreyja : This is a crisis that will lead to destruction and sorrow to the people nearby!",bc_all;
	sleep 10000;
	announce "Valfreyja : As the goddess of this world, I am asking for your cooperation to solve this crisis!",bc_all;
	sleep 10000;
	announce "Valfreyja : The estimated time before the said abnormality, the \"flood\" will emerge in 5 Minutes time!.",bc_all;
	sleep 30000;	//1 Minute passed
	announce "Valfreyja : I won't leave things unrewarded for helping to solve the crisis! Join the fight!",bc_all;
	sleep 20000;
	announce "Valfreyja : Rewards will be based on your participation!.",bc_all;
	sleep 20000;
	announce "Valfreyja : The more damage you dealt to the boss will give you plenty rewards! same as killing more enemies!",bc_all;
	sleep 20000;	//2 Minutes passed
	announce "Valfreyja : 3 more minutes left before it begins! Prepare as fast as you can!",bc_all;
	sleep 30000;	
	announce "Valfreyja : Join the others and help solve the crisis at "+.EventMap$[.MapID]+"!",bc_all;
	sleep 30000;	//3 Minutes passed
	announce "Valfreyja : 2 Minutes left until the flood waves start!",bc_all;
	sleep 30000;
	announce "Valfreyja : There are currently "+getmapusers(.EventMap$[.MapID])+" prepared adventurers!",bc_all;
	sleep 30000;	//4 Minutes passed
	announce "Valfreyja : 1 Minute! Please help defend "+.EventMap$[.MapID]+" from the \"flood\".",bc_all;
	sleep 40000;
	announce "Valfreyja : 20 Seconds left.",bc_all;
	sleep 15000;
	announce "Valfreyja : 5 Seconds left.",bc_all;
	sleep 2000;
	announce "Valfreyja : \"Flood\" will emerge in 3.",bc_all;
	sleep 1000;
	announce "Valfreyja : 2.",bc_all;
	sleep 1000;
	announce "Valfreyja : 1.",bc_all;
	sleep 1000;
	announce "Valfreyja : Good Luck and Bless all participants! wave "+.wave+" is coming!",bc_all;
	callsub OnSlavesSpawn,.wave;
end;

OnPreludeSlave:
	announce "Valfreyja : Good job on repelling wave "+(.wave-1)+"!",bc_all;
	sleep 5000;
	announce "Valfreyja : The next wave will arrive in roughly 10 seconds.",bc_all;
	sleep 7000;
	announce "Valfreyja : Next wave in 3.",bc_all;
	sleep 1000;
	announce "Valfreyja : 2.",bc_all;
	sleep 1000;
	announce "Valfreyja : 1.",bc_all;
	sleep 1000;	
	announce "Valfreyja : Good Luck and Bless all participants! wave "+.wave+" is coming!",bc_all;
return;

OnSlavesSpawn:
	if(.WaveSlaves$[.wave]=="")
		callsub OnWorldBossSpawn;
	.Slaves = 0;
	explode(.@mobs$,.WaveSlaves$[.wave],",");
	for(.@i=0;.@i<getarraysize(.@mobs$);.@i+=2){
		monster .EventMap$[.MapID],0,0,"[ Slave ] "+strmobinfo(1,atoi(.@mobs$[.@i])),atoi(.@mobs$[.@i]),atoi(.@mobs$[.@i+1]),strnpcinfo(0)+"::OnSlaveDead";
		.Slaves += atoi(.@mobs$[.@i+1]);
	}
end;

OnSlaveDead:
	.Slaves--;
	if(isloggedin(killerrid))
		query_sql("INSERT INTO `worldboss` (account_id,char_id,name,kills) VALUES ("+getcharid(3)+","+getcharid(0)+",'"+strcharinfo(0)+"',1) ON DUPLICATE KEY UPDATE kills=kills+1,name='"+strcharinfo(0)+"';");
	if(.Slaves%5 == 0 && .Slaves >=5)
		announce "Valfreyja : "+.Slaves+" Monsters Left.",bc_all;
	if(.Slaves < 5 && .Slaves > 0)
		announce "Valfreyja : "+.Slaves+" Monster"+((.Slaves==1)?"":"s")+" Left.",bc_all;
	if(.Slaves==0)
		if(.WaveSlaves$[++.wave]!=""){
			callsub OnPreludeSlave;
			callsub OnSlavesSpawn,.wave;
		}else{
			callsub OnPreludeBoss;
			callsub OnWorldBossSpawn;
		}
end;

OnPreludeBoss:
	announce "Valfreyja : Wow! That was the last wave of small time monsters!",bc_all;
	sleep 5000;
	announce "Valfreyja : The next one will be the boss now, Prepare for battle!",bc_all;
	sleep 5000;
	announce "Valfreyja : You will be rewarded base on how much damage you dealt.",bc_all;
	sleep 5000;
	announce "Valfreyja : This is no ordinary boss as it has a very immense power than other bosses.",bc_all;
	sleep 5000;
	announce "Valfreyja : Anyway, The Boss is coming now! On your positions!",bc_all;
	sleep 5000;	
	announce "Valfreyja : 3.",bc_all;
	sleep 1000;
	announce "Valfreyja : 2.",bc_all;
	sleep 1000;
	announce "Valfreyja : 1.",bc_all;
	sleep 1000;	
	announce "Valfreyja : Good Luck and Bless all of you!",bc_all;
return;

OnWorldBossSpawn:
	deletearray $@damage_cid;
	.@BossID = rand(getarraysize(.WorldBossID));
	monster .EventMap$[.MapID],.BossSpawnX[.MapID],.BossSpawnY[.MapID],"[ World Boss ] "+strmobinfo(1,.WorldBossID[.@BossID]),.WorldBossID[.@BossID],1,strnpcinfo(0)+"::OnWorldBossDead",2;
end;

OnWorldBossDead:
	set $WBID,gettimetick(2);
	.@name$ = strcharinfo(0);
	freeloop(1);
	for(.@i=0;.@i<getarraysize(.LastHitReward);.@i+=2)
		getitem .LastHitReward[.@i],.LastHitReward[.@i+1];
	for(.@i=0; .@i<getarraysize(.ScatteredItems); .@i+=2){
		for(.@j=0;.@j<.ScatteredItems[.@i+1];.@j++){
			do {
				.@x = rand(0,.MapMaxX[.MapID]);
				.@y = rand(0,.MapMaxY[.MapID]);
			} while (!checkcell(.EventMap$[.MapID],.@x,.@y,cell_chkpass));
			makeitem .ScatteredItems[.@i],1,"this",.@x,.@y;
		}
	}
	if(killedrid)
		for(.@i=0;.@i<getarraysize($@damage_done);.@i++)
			query_sql("INSERT INTO `worldboss` (account_id,char_id,name,damage) SELECT account_id,'"+$@damage_cid[.@i]+"',name,'"+$@damage_done[.@i]+"' FROM `char` WHERE `char`.`char_id`="+$@damage_cid[.@i]+" ON DUPLICATE KEY UPDATE damage = damage + "+$@damage_done[.@i]+"");
	freeloop(0);
	announce "Valfreyja : The Boss has been slain! Congratulations.",bc_all;
	sleep 5000;
	announce "Valfreyja : Brave "+.@name$+" has dealt the last damage!",bc_all;
	sleep 5000;
	.@top = query_sql("SELECT char_id,name,damage FROM worldboss ORDER BY damage DESC LIMIT 3",.@cid,.@name,.@dmg);
	if(.@dmg[2]){
		announce "Valfreyja : Ranking 3rd! Brave "+.@name[2]+" for dealing "+callfunc("F_InsertComma",.@dmg[2])+" damage to the boss !",bc_all; sleep 5000;}
	if(.@dmg[1]){
		announce "Valfreyja : Ranking 2nd! Brave "+.@name[1]+" for dealing "+callfunc("F_InsertComma",.@dmg[1])+" damage to the boss!",bc_all; sleep 5000;}
	if(.@dmg[0]){
		announce "Valfreyja : Lastly! Ranking 1st! Hero "+.@name[0]+" dealing a total damage of "+callfunc("F_InsertComma",.@dmg[0])+" to the boss !",bc_all; sleep 5000;}
	callsub OnEventEnd;
end;

OnEventEnd:
	announce "Valfreyja : The crisis has been solved.",bc_all;
	sleep 10000;
	announce "Valfreyja : I thank you all for helping to subjugate the flood at "+.EventMap$[.MapID]+".",bc_all;
	sleep 10000;	
	announce "Valfreyja : As my thanks, All participants subjugating the boss will be rewarded!",bc_all;
	sleep 10000;
	getmapxy(.@map$,.@x,.@y,BL_NPC,$@WBREWARDGID$);
	announce "Valfreyja : Please Proceed to my guardian at "+.@map$+" "+.@x+","+.@y+"!",bc_all;
	sleep 10000;
	announce "Valfreyja : Once Again, Thank you for helping, I hope to see you adventurers at the next crisis!",bc_all;
end;

OnInit:
	//========Map Configuration=========//
	setarray .EventMap$[0],"kamizama";		//Maps
	setarray .MapMaxX[0],152;				//Map Max X coordinate(For scattering items)
	setarray .MapMaxY[0],154;				//Map Max Y coordinate
	setarray .BossSpawnX[0],0;				//World Boss Spawn Location X, 0 = Random
	setarray .BossSpawnY[0],0;				//World Boss Spawn Location Y, 0 = Random
	//==================================//
	
	//======Monster Configuration=======//
	setarray .WaveSlaves$[1],		//(MobID,Amount)
							"1272,1,1302,10,1200,10,1202,10,1191,20,1193,16",	//Wave 1 Slaves [Dark Lord, Dark Illusion, Zherlthish, Phendark, Mimic, Alarm]
							"1190,1,1507,10,1370,10,1374,10,1379,20,1371,30",	//Wave 2 Slaves [Orc Lord, Bloody Murderer, Succubus, Incubus, Nightmare Terror, Fallen Angel]
							"1832,1,1831,16,1833,16,1837,16,1383,30,1366,20";	//Wave 3 Slaves [Ifrit, Salamander, Kasa, Imp, Explosion, Lava Golem]
	setarray .WorldBossID[0],20920;	//Randomly Pick 1
	setarray .ScatteredItems[0],6635,40,12103,5,12246,2,7615,50;
	setarray .LastHitReward[0],12246,1,7615,10;	//Item ID, Amount{, Item ID, Amount}
	//For Test Purpose
	bindatcmd "worldboss",strnpcinfo(0)+"::OnEventReady",99,99;
end;

}

darkmall,89,109,3	script	WorldBoss Valfreyja#WBREWARD	936,{

	mes .name$;
	mes "Good Day Fellow~";
	mes "Goddess Freyja has sent me here to give rewards for the braves that help in solving flood crisis that happened a while ago.";
	next;
	switch(select("^00D323~Boss Damage Ranking^000000:^FF0000~Wave Kill Ranking^000000:^0000FF~Check Rewards^000000:~Information:~Goodbye!")){
		case 1:
			mes .name$;
			mes "Here are the rankings";
			mes "for dealing damage on";
			mes "world boss.";
			next;
			callsub OnCheckRanking,1;
			
			break;
		case 2:
			mes .name$;
			mes "Here are the rankings";
			mes "for the amount of kills";
			mes "during the flood.";
			next;
			callsub OnCheckRanking,0;
			break;
		case 3:
			mes .name$;
			mes "^00D323Boss Damage Ranking Rewards~^000000";
			for(.@i=0;.@i<getarraysize(.BossRankRewards$);.@i++){
				deletearray .@brwd$;
				explode(.@brwd$,.BossRankRewards$[.@i],",");
				mes "^FF0000Top "+(.@i+1)+" Rewards: ^000000";
				for(.@j=0;.@j<getarraysize(.@brwd$);.@j+=2)
					mes "->^0000FF"+atoi(.@brwd$[.@j+1])+"x "+getitemname(atoi(.@brwd$[.@j]))+"^000000";
			}
			mes "^FF0000Other Participants Rewards: ^000000";
			for(.@i=0;.@i<getarraysize(.BossConsolation);.@i+=2)	
				mes "->^0000FF"+.BossConsolation[.@i+1]+"x "+getitemname(.BossConsolation[.@i])+"^000000";
			next;
			mes .name$;
			mes "^00D323Wave Kills Ranking Rewards~^000000";
			for(.@i=0;.@i<getarraysize(.WaveRankRewards$);.@i++){
				deletearray .@wrwd$;
				explode(.@wrwd$,.WaveRankRewards$[.@i],",");
				mes "^FF0000Top "+(.@i+1)+" Rewards: ^000000";
				for(.@j=0;.@j<getarraysize(.@wrwd$);.@j+=2)
					mes "->^0000FF"+atoi(.@wrwd$[.@j+1])+"x "+getitemname(atoi(.@wrwd$[.@j]))+"^000000";
			}
			mes "^FF0000Other Participants Rewards: ^000000";
			for(.@i=0;.@i<getarraysize(.WaveConsolation);.@i+=2)
				mes "->^0000FF"+.WaveConsolation[.@i+1]+"x "+getitemname(.WaveConsolation[.@i])+"^000000";
			next;
			mes .name$;
			mes "Are you here to receive some rewards?";
			next;
			switch(select("~Get Boss Damage Rewards:~Get Wave Kills Rewards:~Cancel")){
				case 1:
					if($WBID == 143){ mes .name$; mes "World Boss is currently ongoing.";  end; }
					callsub OnGetBossRewards;
					break;
				case 2:
					if($WBID == 143){ mes .name$; mes "World Boss is currently ongoing.";  end; }
					callsub OnGetWaveRewards;
					break;
				case 3:
					end;
			}
			break;
		case 4:
			mes .name$;
		mes "[^FF0000 World Boss - Valfreyja Information  ^000000]";
		mes "^FF0000[1]^000000 3 Waves to elimate.";
		mes "^FF0000[2]^000000 Must be level 99 to participate the damage rank.";
		mes "^FF0000[3]^000000 You must kill all the mobs each wave to be able to continue.";
		mes "^FF0000[4]^000000 Scattered items when World Boss dies: 40x ^FF0000Blacksmith's Blessing^000000 / 5x ^FF0000Bloody Branch^000000 / 30x ^FF0000MVP Voucher^000000 and 2x ^FF0000Mystical Card Album^000000.";
		mes "^FF0000[5]^000000 Last hitting the World Boss will get 1x ^FF0000Mystical Card Album^000000 & 5x ^FF0000MVP Voucher^000000 .";
			break;
		case 5:
			mes .name$;
			mes "Alright~";
			mes "I'm hoping to see you once again soon.";
			mes "good luck on your adventures.";
			break;
	}
end;

OnCheckRanking:	//Checking the Ranking (DONE)
	if(getarg(0))
		.@ranking = query_sql("SELECT char_id,name,damage FROM worldboss WHERE damage !=0 ORDER BY damage DESC Limit 30",.@cid,.@name$,.@damage);
	else
		.@ranking = query_sql("SELECT char_id,name,kills FROM worldboss WHERE kills !=0 ORDER BY kills DESC Limit 30",.@cid,.@name$,.@kills);
	mes .name$;
	if(!.@cid[0]){mes "I'm sorry but there's no one in the ranking right now"; end;}
	mes "^0000FFRank^000000 - "+(getarg(0)?"^00D323Damage Done^000000":"^FF0000Monster Kills^000000");
	for(.@i=0;.@i<getarraysize(.@cid);.@i++){
		if(.@i%10==0 && .@i!=0){ next; mes .name$; mes "^0000FFRank^000000 - "+(getarg(0)?"^00D323Damage Done^000000":"^FF0000Monster Kills^000000");}
		mes (.@i+1)+". ^0000FF"+.@name$[.@i]+"^000000 - "+(getarg(0)? "^00D323"+callfunc("F_InsertComma",.@damage[.@i])+"^000000":"^FF0000"+.@kills[.@i])+"^000000";
	}
return;

OnGetBossRewards:
	if($WBID==#BossReward){
		mes .name$;
		mes "You already receive your boss reward, Please don't be greedy~";
		end;
	}
	.@idx = callsub(OnCheckReward,1);	//Check if Player on rank
	//IF TRUE = DO BELOW
	if(.BossRankRewards$[.@idx]!=""){	//Item Reward
		explode(.@rwd$,.BossRankRewards$[.@idx],",");
		for(.@i=0;.@i<getarraysize(.@rwd$);.@i+=2)
			getitem atoi(.@rwd$[.@i]),atoi(.@rwd$[.@i+1]);
	}
	else
		for(.@i=0;.@i<getarraysize(.BossConsolation);.@i+=2)
			getitem .BossConsolation[.@i],.BossConsolation[.@i+1];
	callsub OnPercentageReward;	//Point Reward
	callsub OnRewardSuccess,1;
return;

OnPercentageReward:
	query_sql("SELECT ROUND(((SELECT damage FROM worldboss WHERE account_id="+getcharid(3)+") * 100.0 / (SELECT SUM(damage) FROM worldboss)*"+.pointReward+"/100),3) As Points, ROUND(((SELECT damage FROM worldboss WHERE account_id="+getcharid(3)+") * 100.0 / (SELECT SUM(damage) FROM worldboss)*"+.BaseExpReward+"/100),3) As BaseExp, ROUND(((SELECT damage FROM worldboss WHERE account_id="+getcharid(3)+") * 100.0 / (SELECT SUM(damage) FROM worldboss)*"+.JobExpReward+"/100),3) As JobExp",.@points,.@baseExp,.@jobExp);
	if(.@points > .pointMax)
		set .@pointReward,.pointMax;
	else
		set .@pointReward,.@points;
	setd .pointVariable$,getd(.pointVariable$)+.@pointReward;
	dispbottom "You receive "+.@pointReward+" "+.pointName$+".";
	getexp .@baseExp,.@jobExp;	//Exp Reward
	dispbottom "You received "+.@baseExp+" base experience and "+.@jobExp+" job experience.";
return;

OnGetWaveRewards:	//Wave Ranking Rewards (DONE)
	if($WBID==#WaveReward){
		mes .name$;
		mes "You already receive your wave reward, Please don't be greedy~";
		end;
	}
	.@idx = callsub(OnCheckReward,0);	//Check if Player on rank
	if(.WaveRankRewards$[.@idx]!=""){	//Item Reward
		explode(.@rwd$,.WaveRankRewards$[.@idx],",");
		for(.@i=0;.@i<getarraysize(.@rwd$);.@i+=2)
			getitem atoi(.@rwd$[.@i]),atoi(.@rwd$[.@i+1]);
	}
	else
		for(.@i=0;.@i<getarraysize(.WaveConsolation);.@i+=2)
			getitem .WaveConsolation[.@i],.WaveConsolation[.@i+1];
	callsub OnRewardSuccess,0;
return;

OnCheckReward:
	freeloop(1);
	if(getarg(0))
		.@rank = query_sql("SELECT char_id FROM worldboss WHERE damage !=0 ORDER BY damage DESC",.@cid);
	else
		.@rank = query_sql("SELECT char_id FROM worldboss WHERE kills !=0 ORDER BY kills DESC",.@cid);
	freeloop(0);
	.@idx = inarray(.@cid,getcharid(0));
	if(.@idx>=0)
		return .@idx;
	mes .name$;
	mes "Sorry, but it seems like you are not qualified to receive a reward.";
end;

OnRewardSuccess:
	if(getarg(0)) set #BossReward,$WBID;
	else set #WaveReward,$WBID;
	mes .name$;
	mes "Here's your rewards!";
	mes "Thank you for helping the flood crisis";
end;

OnInit:
	//========START OF WAVE REWARDS CONFIGURATION=========//
	setarray .WaveRankRewards$[0],		//Individual Rewards for Top 3 Player
							"14003,40",	//TOP 1
							"14003,32",	//TOP 2
							"14003,24",	//TOP 3
							"14003,16",	//TOP 4
							"14003,8";	//FOR TOP 5
	setarray .WaveConsolation[0],7615,4;//FOR PLAYERS NOT IN TOP
	//=========END OF WAVE REWARDS CONFIGURATION==========//
	
	//========START OF BOSS REWARDS CONFIGURATION=========//
	setarray .BossRankRewards$[0],		//Individual Rewards for Top 3 Player
							"7615,25",	//TOP 1
							"7615,20",	//TOP 2
							"7615,15",	//TOP 3
							"7615,10",	//TOP 4
							"7615,5";	//FOR TOP 5
	setarray .BossConsolation[0],14003,3;//FOR PLAYERS NOT IN TOP
	//==========POINTS REWARD
	set .pointVariable$,"#CASHPOINTS";
	set .pointName$,"Cash Points";
	set .pointReward,1000;	// Whole Reward that will be distributed based on boss damage by percentage %	(Must be 100 Above)
	set .pointMax,250;	//Max Reward for players(To prevent taking all the Point Reward)
	
	//==========ETC Rewards
	set .BaseExpReward,0;	// For Experience Reward based on Damage by percentage(Max of 2.1B Experience)
	set .JobExpReward,0;
	
	//=========END OF BOSS REWARDS CONFIGURATION=========//
	
	//DO NOT TOUCH
	set .name$,"[ Lenneth ]";
	set $@WBREWARDGID$,strnpcinfo(0);
end;
}