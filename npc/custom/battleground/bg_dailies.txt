-	script	Battleground Dailies	-1,{
	// Initialization
	.@char_id = getcharid(0);
	.@account_id = getcharid(3);
	.@current_date$ = gettimestr("%Y-%m-%d", 21); // 2020-01-01
	.@wins = 0;
	.@claimed = 0;
	
	// Check if the player using the NPC is logged in.
	if (isloggedin(.@account_id,.@char_id)) {
		.@login_ip$ = getcharip(.@char_id); // get the login IP for IP checking.
		// Check if the IP restriction is enabled then we will set the check SQL script for it.
		if (.ip_restriction)
			.@check_sql$ = "SELECT `win`, `claimed` FROM `bg_daily_wins` WHERE `last_ip` = '" + .@login_ip$ + "' AND DATE(`created_at`) = '" + .@current_date$ + "';";
		else
			.@check_sql$ = "SELECT `win`, `claimed` FROM `bg_daily_wins` WHERE `account_id` = " + .@account_id + " AND DATE(`created_at`) = '" + .@current_date$ + "' AND `last_ip` = '" + .@login_ip$ +"';";
			
		if (query_sql(.@check_sql$, .@wins, .@claimed) < 1) {
			// No record for today then we insert a new record
			query_sql("INSERT INTO `bg_daily_wins`(`account_id`, `last_ip`, `created_at`) VALUES (" + .@account_id + ",'" + .@login_ip$ + "','" + .@current_date$ + "')");
		} else {
			// Fetch and evaluate if the user is qualified to get the dailies. We will perform a double check on IP restriction here.
			if (! .ip_restriction)
				query_sql("SELECT `win`, `claimed` FROM `bg_daily_wins` WHERE `account_id` = " + .@account_id + " AND DATE(`created_at`) = '" + .@current_date$ + "';", .@wins, .@claimed);
			else {
				query_sql("SELECT SUM(`win`) as `win`, `claimed` FROM `bg_daily_wins` WHERE `last_ip` = '" + .@login_ip$ + "' AND DATE(`created_at`) = '" + .@current_date$ + "' ORDER BY `updated_at`;", .@wins, .@claimed);
			}
		}
	} else {
		mes .n$;
		mes "This is an invalid request.";
		mes "Are you a magician?";
		close;
	}// if logged in
	dispbottom "Your Total BG Wins for today (Server Date: " + .@current_date$ + "): " + .@wins + " win(s).";

	// Process start
	mes .n$;
	mes "Greetings, I'm in-charge of taking notes of how many wins you made so far here.";
	next;
	mes .n$;
	mes "I give Daily Rewards for ^1ABC9C" + .bg_dailies_wins + "^000000 BG wins!";
	mes "------";
	mes "What's your agenda with me now?";
	.@select = select("Check my dailies progress:Claim my BG daily win rewards:Cancel");
	next;

	switch(.@select) {
		case 1:
			mes .n$;
			mes "Let's see..";
			next;
			mes .n$;
			mes "Your total wins for today is ^E74C3C" + .@wins + "^000000.";
			close;
			break;
		case 2:
			mes .n$;
			mes "Hmm, let's see..";
			// mes "Your total wins for today is ^E74C3C" + .@wins + "^000000 wins.";
			next;
			if (.@wins >= .bg_dailies_wins) {
				mes .n$;
				if (.@claimed) {
					mes "You have claimed your daily rewards for today.";
					mes "Come back again tomorrow.";
					close;
				}
				mes "Here are your BG daily rewards!";
				mes "Come back again tomorrow!";
				if (.ip_restriction)
					.@update_sql$ = "UPDATE `bg_daily_wins` SET `claimed` = 1 WHERE `last_ip` = '" + .@login_ip$ + "' AND DATE(`created_at`) = '" + .@current_date$ + "';";
				else
					.@update_sql$ = "UPDATE `bg_daily_wins` SET `claimed` = 1 WHERE `account_id` = " + .@account_id + " AND DATE(`created_at`) = '" + .@current_date$ + "';";

				query_sql(.@update_sql$);

				// Default reward on BG dailies.
				for(.@i = 0; .@i < getarraysize(.bg_dailies_rewards); .@i += 1) {
					if (.bg_dailies_rewards[.@i] != 14652)
						getitem .bg_dailies_rewards[.@i],.bg_dailies_rewards_qty[.@i];
					else
						getitem .bg_dailies_rewards[.@i],.@wins; // Reward Ares' Gift depending on the number of wins accumulated by the player.
				} // for

				callfunc "determine_tier_rewards",.@wins,.@account_id;
				end;
			} else {
				.@wins_need = (.bg_dailies_wins - .@wins);
				mes .n$;
				mes "You still need ^2471A3" + .@wins_need + "^000000 wins to claim your dailies.";
			}
			close;
			break;
		case 3:
			mes .n$;
			mes "Hmm.. Alright";
			close;
			break;
	}
	close;


OnInit:
	set .n$, "[Battleground Dailies]";
	set .bg_dailies_wins, 10; // Needed win to claim dailies
	set .ip_restriction,1; // IP Restriction - Set 1 to enable / set 0 to disable


	setarray .bg_dailies_rewards[0],7179,7829,6187,14652; // Rewards in item ID
	setarray .bg_dailies_rewards_qty[0],10,1000,2500,10; // Quantity of rewards


	waitingroom "BG Dailies", 0;

}

function	script	determine_tier_rewards	{
	if (getargcount() != 2) return;

	// TODO: Give an amount of loot box here:
	setarray .bg_dailies_rewards_tier1[0],7773,14655;
	setarray .bg_dailies_rewards_tier1_qty[0],10,1;

	setarray .bg_dailies_rewards_tier2[0],7773,14655,14656;
	setarray .bg_dailies_rewards_tier2_qty[0],25,2,1;

	setarray .bg_dailies_rewards_tier3[0],7773,14655,14656,14657;
	setarray .bg_dailies_rewards_tier3_qty[0],50,3,2,1;

	.@wins = getarg(0);
	.@account_id = getarg(1);

	// Here where the Tier Rewards are being evaluated.
	dispbottom "Total wins when reward claimed: " + .@wins;
	if (.@wins >= 20 && .@wins < 30) {
		for(.@i = 0; .@i < getarraysize(.bg_dailies_rewards_tier1); .@i += 1) {
			getitem .bg_dailies_rewards_tier1[.@i],.bg_dailies_rewards_tier1_qty[.@i],.@account_id;
		} // for loop bg tier 1
		dispbottom "You have claimed the Tier I BG Reward. Please come back again tomorrow.";
	}

	else if (.@wins >= 30 && .@wins < 50) {
		for(.@i = 0; .@i < getarraysize(.bg_dailies_rewards_tier2); .@i += 1) {
			getitem .bg_dailies_rewards_tier2[.@i],.bg_dailies_rewards_tier2_qty[.@i],.@account_id;
		} // for loop bg tier 2
		dispbottom "You have claimed the Tier II BG Reward. Please come back again tomorrow.";
	}

	else if (.@wins >= 50) {
		for(.@i = 0; .@i < getarraysize(.bg_dailies_rewards_tier3); .@i += 1) {
			getitem .bg_dailies_rewards_tier3[.@i],.bg_dailies_rewards_tier3_qty[.@i],.@account_id;
		} // for loop bg tier 3
		dispbottom "You have claimed the most highest tier of BG Reward. Please come back again tomorrow.";
	}
	close;
	return;
} // determine_tier_rewards

bat_room,147,142,4	duplicate(Battleground Dailies)	Battleground Ground#bat_room	417