-	script	Battleground Dailies	-1,{
	// Initialization
	.@char_id = getcharid(0);
	.@account_id = getcharid(3);
	.@current_date$ = gettimestr("%Y-%m-%d", 21); // 2020-01-01

	// Check if the player using the NPC is logged in.
	if (isloggedin(.@account_id,.@char_id)) {
		.@login_ip$ = getcharip(.@char_id); // get the login IP for IP checking.
		// Check if the IP restriction is enabled then we will set the check SQL script for it.
		if (.ip_restriction)
			.@check_sql$ = "SELECT `win`, `claimed` FROM `bg_daily_wins` WHERE `last_ip` = '" + .@login_ip$ + "' AND DATE(`created_at`) = '" + .@current_date$ + "';";
		else
			.@check_sql$ = "SELECT `win`, `claimed` FROM `bg_daily_wins` WHERE `account_id` = " + .@account_id + " AND DATE(`created_at`) = '" + .@current_date$ + "' AND `last_ip` = '" + .@login_ip$ +"';";
		
		if (query_sql(.@check_sql$, .@wins, .@claimed) < 1) {
			// No record for today then we insert a new record
			query_sql("INSERT INTO `bg_daily_wins`(`account_id`, `last_ip`, `created_at`) VALUES (" + .@account_id + ",'" + .@login_ip$ + "','" + .@current_date$ + "')");
			.@wins = 0;
			.@claimed = 0;
		} else {
			// Fetch and evaluate if the user is qualified to get the dailies. We will perform a double check on IP restriction here.
			if (! .ip_restriction)
				query_sql("SELECT `win`, `claimed` FROM `bg_daily_wins` WHERE `account_id` = " + .@account_id + " AND DATE(`created_at`) = '" + .@current_date$ + "';", .@wins, .@claimed);
			else {
				query_sql("SELECT `win`, `claimed`, count(*) as `claim_count` FROM `bg_daily_wins` WHERE `last_ip` = '" + .@login_ip$ + "' AND DATE(`created_at`) = '" + .@current_date$ + "';", .@wins, .@claimed, .@claim_count);
				if (.@claim_count > 1) {
					.@claimed = 1; // Set it already claimed.
				}
			}
		}
	} else {
		mes .n$;
		mes "This is an invalid request.";
		mes "Are you a magician?";
		close;
	}// if logged in
	dispbottom "Your Total BG Wins for today (Server date: " + .@current_date$ + "): " + .@wins + " win(s).";

	// Process start
	mes .n$;
	mes "Greetings, I'm in-charge of taking notes of how many wins you made so far here.";
	next;
	mes .n$;
	mes "I give Daily Rewards for ^1ABC9C" + .bg_dailies_wins + "^000000 BG wins!";
	mes "------";
	mes "What's your agenda with me now?";
	.@select = select("Check & claim my BG daily win rewards:Cancel"); // TODO: Add participation rewards
	next;

	switch(.@select) {
		case 1:
			mes .n$;
			mes "Hmm, let's see..";
			mes "Your total wins for today is ^E74C3C" + .@wins + "^000000 wins.";
			next;
			if (.@wins >= .bg_dailies_wins) {
				mes .n$;
				if (.@claimed) {
					mes "You have claimed your daily rewards for today.";
					mes "Come back again tomorrow.";
					close;
				}
				mes "Here are your BG daily rewards!";
				mes "Come back again tomorrow!";
				if (.ip_restriction)
					.@update_sql$ = "UPDATE `bg_daily_wins` SET `claimed` = 1 WHERE `last_ip` = '" + .@login_ip$ + "' AND DATE(`created_at`) = '" + .@current_date$ + "';";
				else
					.@update_sql$ = "UPDATE `bg_daily_wins` SET `claimed` = 1 WHERE `account_id` = " + .@account_id + " AND DATE(`created_at`) = '" + .@current_date$ + "';";

				query_sql(.@update_sql$);

				for(.@i = 0; .@i < getarraysize(.bg_dailies_rewards); .@i += 1) {
					getitem .bg_dailies_rewards[.@i],.bg_dailies_rewards_qty[.@i];
				} // for
			} else {
				.@wins_need = (.bg_dailies_wins - .@wins);
				mes .n$;
				mes "You still need ^2471A3" + .@wins_need + "^000000 wins to claim your dailies.";
			}
			close;
			break;
		case 2:
			mes .n$;
			mes "Hmm.. Alright";
			close;
			break;
	}
	close;


OnInit:
	set .n$, "[Battleground Dailies]";
	set .bg_dailies_wins, 10; // Needed win to claim dailies
	set .ip_restriction,1; // IP Restriction - Set 1 to enable / set 0 to disable
	setarray .bg_dailies_rewards[0],7179,7829,6187,14652; // Rewards in item ID
	setarray .bg_dailies_rewards_qty[0],10,1000,2500,1; // Quantity of rewards
	waitingroom "BG Dailies", 0;
}

bat_room,147,142,4	duplicate(Battleground Dailies)	Battleground Ground#bat_room	417