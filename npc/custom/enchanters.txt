// bringing the enchanter to Jin

askald,21,168,5	script	Dylan	4_CAT_SAILOR5,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "It seems difficult to proceed any further since the items in your possession are too heavy. Reduce the weight and try again.";
		close;
	}
	if (checkweight(501,1) == 0) {
		mes "There are too many kinds of items in possession. Reduce the type of the items and try again.";
		close;
	}
	if (isbegin_quest(12369) == 0) {
		mes "[Dylan]";
		mes "Are you a customer? I am not going to get more customers today because I am tired.";
		next;
		select("Are you running a business in the prison?");
		mes "[Dylan]";
		mes "Although I may look like this now, I used to be called the man with the hand of minus out there. It is quite dangerous stuff. You'd better stay away.";
		next;
		mes "^0000ffUnlike his words, he really seems eager to give an explanation.^000000";
		next;
		if (select( "I don't want to waste time here.", "What kind of business you are running here." ) == 1) {
			mes "[Dylan]";
			mes "Don't be a chicken...";
			close;
		}
		mes "[Dylan]";
		mes "Honor Mark, which is issued by the kingdom and not to be traded between individuals. I receive it and enhance people's equipment.";
		next;
		mes "[Dylan]";
		mes "As you can tell, it is against the law. That's why I used to only work for through personal contacts. However, one guy whose equipment was broken while resetting the enhancement informed on me.";
		next;
		select("It really is a crime.");
		mes "[Dylan]";
		mes "I don't need to hear of some personal impression by you. Although I am caught, I am roaming the prison freely thanks to the warder over there.";
		next;
		mes "[Dylan]";
		mes "Of course, I cannot get out to avoid causing the real problem. That's why I stay in the prison like this. Are you interested in the deal?";
		next;
		if (select( "The kingdom has poor public security.", "What is the deal?" ) == 1) {
			mes "[Dylan]";
			mes "What a nerd. I told you to back off if you didn't want to get involved.";
			close;
		}
		mes "[Dylan]";
		mes "You are a great fellow to deal with. I want to save enough money to bail myself out. Any customers are welcome here.";
		next;
		mes "[Dylan]";
		mes "I enhance the three items, Flattery Robe, Invective Robe and Prontera Badge for Honor Mark.";
		next;
		mes "[Dylan]";
		mes "I guess I don't need to explain about it anymore. Just bring the equipment you want to enhance to me.";
		setquest 12369;// Secret Business Relation
		completequest 12369;// Secret Business Relation
		close;
	}
	mes "[Dylan]";
	mes "Are you here to deal with me? Tell me what you need now.";
	next;
	.@s = select( "Tell me about the deal.", "Enhance Robe", "Enhance Badge", "Reset Enhanced Robe Stats", "Reset Enhanced Badge Stats" );
	switch(.@s) {
	case 1:
		mes "[Dylan]";
		mes "Listen carefully. I won't say twice.";
		next;
		mes "[Dylan]";
		mes "I'll enhance one of the Flattery Robe or Invective Robe slot for 20 Honor Marks. I can enhance up to 2 slots. And I take 10 Honor Marks for the reset. Do you follow me so far?";
		next;
		mes "[Dylan]";
		mes "Next, for the Prontera Badge. I'll take 5 Honor Marks to enhance 1 slot. I can enhance up to 2 slots. And I take 10 Honor Marks for the reset.";
		next;
		mes "[Dylan]";
		mes "I don't perform the reset anytime. I'll do it when the 2 slots are enhanced for that you cannot enhance the item anymore. And... Then...";
		next;
		mes "[Dylan]";
		mes "Oh, there is one problem though. There is a change that the badge can be destroyed while resetting it. The chance is about 20%. Since it is such a tiny thing, there is some side effect of removing the enhancement.";
		next;
		mes "[Dylan]";
		mes "I hope the deal goes well, buddy.";
		close;
	case 2:
		mes "[Dylan]";
		mes "You need 20 Honor Marks for each slot of Flattery Robe or Invective Robe. Do you want to enhance it?";
		break;
	case 3:
		mes "[Dylan]";
		mes "You need 5 Honor Marks to enhance one slot of the Prontera Badge you are wearing in the left slot of your body. Would you enhance it?";
		break;
	case 4:
		mes "[Dylan]";
		mes "You need 10 Honor Marks to reset Invective Robe or Flattery Robe. The equipment won't be destroyed. The bound cards and the refinement level would remain the same as well.";
		break;
	case 5:
		mes "[Dylan]";
		mes "You need 10 Honor Marks to reset the Prontera Badge you are wearing in the left slot of your body.";
		next;
		mes "[Dylan]";
		mes "As I told you, it is difficult to reset the badge, ^ff0000The chance of success is 80%.^000000 If it fails, everything will be gone. Would you reset it?";
		break;
	}
	next;
	if (select( "I quit.", "Let's do it now." ) == 1) {
		mes "[Dylan]";
		mes "I am busy so please don't waste my time by talking to me for no reason.";
		close;
	}
	switch(.@s) {
	case 2:
		goto Enchant_Armor;
		break;
	case 3:
		if (getequipid(EQI_ACC_L) == -1) {
			mes "[Dylan]";
			mes "Oh, I have forgotten to mention it. I only enhance the item equipped in the left slot. Enhance it while equipping it in the left slot.";
			close;
		}
		if (getequipid(EQI_ACC_L) != 28356) {
			mes "[Dylan]";
			mes "Stop fooling around. If you want to enhance it, come to me wearing the Prontera Badge.";
			close;
		}
		.@card[2] = getequipcardid(EQI_ACC_L,2);
		.@card[3] = getequipcardid(EQI_ACC_L,3);
		if (.@card[2] > 0 && .@card[3] > 0) {// note: unslotted
			mes "[Dylan]";
			mes "It cannot be enhanced anymore. Do you have anything else to enhance?";
			close;
		}
		if (countitem(6919) < 5) {
			mes "[Dylan]";
			mes "Hey, buddy. You cannot deal with me if you don't have any Honor Marks.";
			close;
		}
		.@card[3] = getequipcardid(EQI_ACC_L,3);
		if (.@card[3] == 0) {
			setarray .@enhance[0],
				 37, 4710,	// INT+1
				 57, 4730,	// AGI+1
				 76, 4740,	// VIT+1
				 91, 4720,	// DEX+1
				104, 4750,	// LUK+1
				113, 4700,	// STR+1
				120, 4711,	// INT+2
				122, 4721,	// DEX+2
				124, 4701,	// STR+2
				126, 4731,	// AGI+2
				128, 4751,	// LUK+2
				129, 4741,	// VIT+2
				130, 4752,	// LUK+3
				131, 4702,	// STR+3
				132, 4732,	// AGI+3
				133, 4712,	// INT+3
				134, 4722,	// DEX+3
				135, 4742;	// VIT+3
			.@card_index = 3;
		}
		else {
			setarray .@enhance[0],
				 37, 4740,	// VIT+1
				 63, 4710,	// INT+1
				 81, 4750,	// LUK+1
				 96, 4700,	// STR+1
				105, 4720,	// DEX+1
				111, 4730,	// AGI+1
				116, 4751,	// LUK+2
				120, 4711,	// INT+2
				122, 4721,	// DEX+2
				124, 4741,	// VIT+2
				126, 4701,	// STR+2
				128, 4731,	// AGI+2
				129, 4742,	// VIT+3
				130, 4752,	// LUK+3
				131, 4702,	// STR+3
				132, 4732,	// AGI+3
				133, 4712,	// INT+3
				134, 4722;	// DEX+3
			.@card_index = 2;
		}
		.@size = getarraysize(.@enhance);
		.@r = rand( 1,.@enhance[.@size-2] );
		for ( .@i = 0; .@i < .@size && .@enhance[.@i] < .@r; .@i += 2 )
			continue;
		.@card[ .@card_index ] = .@enhance[.@i+1];

		delitem 6919,5;
		delequip EQI_ACC_L;
		getitem2 28356,1,1,0,0,0,0,.@card[2],.@card[3];
		break;
	case 4:
		.@item_id = getequipid(EQI_ARMOR);
		.@refine = getequiprefinerycnt(EQI_ARMOR);
		.@card[0] = getequipcardid(EQI_ARMOR,0);
		.@card[2] = getequipcardid(EQI_ARMOR,2);
		.@card[3] = getequipcardid(EQI_ARMOR,3);
		if (.@item_id == -1) {
			mes "[Dylan]";
			mes "You are taking it off.";
			close;
		}
		if (.@item_id != 15146 && .@item_id != 15147) {
			mes "[Dylan]";
			mes "Stop fooling around. If you want to enhance it, come to me wearing the equipment.";
			close;
		}
		if (.@card[2] == 0 || .@card[3] == 0) {
			mes "[Dylan]";
			mes "Oh, this item is not enhanced completely. I cannot reset it yet.";
			close;
		}
		if (countitem(6919) < 10) {
			mes "[Dylan]";
			mes "Hey, buddy. You cannot deal with me if you don't have any Honor Marks.";
			close;
		}
		delitem 6919,10;
		delequip EQI_ARMOR;
		getitem2 .@item_id,1,1,.@refine,0,.@card[0],0,0,0;
		break;
	case 5:
		if (getequipid(EQI_ACC_L) == -1) {
			mes "[Dylan]";
			mes "Oh, I have forgotten to mention it. I only enhance the item equipped in the left slot. Enhance it while equipping it in the left slot.";
			close;
		}
		.@card[2] = getequipcardid(EQI_ACC_L,2);
		.@card[3] = getequipcardid(EQI_ACC_L,3);
		if (.@card[2] == 0 || .@card[3] == 0) {// note: Prontera Badge unslotted
			mes "[Dylan]";
			mes "Oh, this item is not enhanced completely. I cannot reset it yet.";
			close;
		}
		if (getequipid(EQI_ACC_L) != 28356) {
			mes "[Dylan]";
			mes "Stop fooling around. If you want to enhance it, come to me wearing the Prontera Badge.";
			close;
		}
		if (countitem(6919) < 10) {
			mes "[Dylan]";
			mes "Hey, buddy. You cannot deal with me if you don't have any Honor Marks.";
			close;
		}
		delequip EQI_ACC_L;
		delitem 6919,10;
		mes "[Dylan]";
		if (rand(1,100) < 80) {
			mes "Hum, it has been processed well. Check out the item.";
			getitem 28356,1;// Prontera Badge
			specialeffect2 EF_REPAIRWEAPON;
		}
		else {
			mes "I guess you are out of luck. The equipment is broken. Tut tut.";
			getitem 6920,14;// Rune_Magic_Powder
			specialeffect2 EF_LORD;
		}
		close;
	}
	mes "[Dylan]";
	mes "Hum, it has been processed well. Check out the item.";
	specialeffect2 EF_REPAIRWEAPON;
	close;


Enchant_Armor:
	.@item_id = getequipid(EQI_ARMOR);
	.@refine = getequiprefinerycnt(EQI_ARMOR);
	.@card[0] = getequipcardid(EQI_ARMOR,0);
	.@card[1] = getequipcardid(EQI_ARMOR,1);
	.@card[2] = getequipcardid(EQI_ARMOR,2);
	.@card[3] = getequipcardid(EQI_ARMOR,3);

	if (.@item_id == -1) {
		mes "[Dylan]";
		mes "You are taking it off.";
		close;
	}
	if (.@item_id != 15146 && .@item_id != 15147) {
		mes "[Dylan]";
		mes "Stop fooling around. If you want to enhance it, come to me wearing the equipment.";
		close;
	}
	if (.@card[2] > 0 && .@card[3] > 0) {
		mes "[Dylan]";
		mes "You already have enchants on this.";
		mes "Do you want to continue and re-enchant both enchants?";
		mes "After that I'll enchant it for the first slot!";
		switch(select("Yes:No")) {
			case 1:
				.@card[2] = 0;
				.@card[3] = 0;
				delequip EQI_ARMOR;
				getitem2 .@item_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
				equip .@item_id;
				goto Enchant_Armor;
			case 2:
				mes "Okay. Good bye. Come back next time.";
				close;
		}
	//	mes "It cannot be enhanced anymore. Do you have anything else to enhance?";
	//	close;
	}

	if (countitem(6919) < 20) {
		mes "[Dylan]";
		mes "Hey, buddy. You cannot deal with me if you don't have any Honor Marks.";
		close;
	}

	if (.@item_id == 15147) {// Abusive Robe
		if (.@card[3] == 0) {
			setarray .@enhance[0],
				25,   4994,	// Rune of Strength Lv 1
				105,  4997,	// Rune of Agility Lv 1
				110, 29009,	// Rune of Vitality Lv 1
				170, 29000,	// Rune of Intellect Lv 1
				230, 29003,	// Rune of Dexterity Lv 1
				255, 29006,	// Rune of Luck Lv 1
				265,  4995,	// Rune of Strength Lv 2
				315,  4998,	// Rune of Agility Lv 2
				317, 29010,	// Rune of Vitality Lv 2
				352, 29001,	// Rune of Intellect Lv 2
				387, 29004,	// Rune of Dexterity Lv 2
				407, 29007,	// Rune of Luck Lv 2
				412,  4996,	// Rune of Strength Lv 3
				432,  4999,	// Rune of Agility Lv 3
				452, 29002,	// Rune of Intellect Lv 3
				467, 29005,	// Rune of Dexterity Lv 3
				477, 29008;	// Rune of Luck Lv 3
			.@card_index = 3;
		}
		else {
			setarray .@enhance[0],
				60,   4994,	// Rune of Strength Lv 1
				80,  29000,	// Rune of Intellect Lv 1
				160, 29009,	// Rune of Vitality Lv 1
				180, 29003,	// Rune of Dexterity Lv 1
				220, 29006,	// Rune of Luck Lv 1
				270,  4995,	// Rune of Strength Lv 2
				275,  4998,	// Rune of Agility Lv 2
				325, 29010,	// Rune of Vitality Lv 2
				350, 29001,	// Rune of Intellect Lv 2
				365, 29004,	// Rune of Dexterity Lv 2
				405, 29007,	// Rune of Luck Lv 2
				420,  4996,	// Rune of Strength Lv 3
				425,  4999,	// Rune of Agility Lv 3
				435, 29002,	// Rune of Intellect Lv 3
				455, 29011,	// Rune of Vitality Lv 3
				465, 29005,	// Rune of Dexterity Lv 3
				480, 29008;	// Rune of Luck Lv 3
			.@card_index = 2;
		}
	}
	else {// Flattery Robe
		if (.@card[3] == 0) {
			setarray .@enhance[0],
				50,   4994,	// Rune of Strength Lv 1
				100,  4997,	// Rune of Agility Lv 1
				150, 29009,	// Rune of Vitality Lv 1
				190, 29000,	// Rune of Intellect Lv 1
				230, 29003,	// Rune of Dexterity Lv 1
				270, 29006,	// Rune of Luck Lv 1
				305,  4995,	// Rune of Strength Lv 2
				340,  4998,	// Rune of Agility Lv 2
				390, 29010,	// Rune of Vitality Lv 2
				425, 29001,	// Rune of Intellect Lv 2
				460, 29004,	// Rune of Dexterity Lv 2
				495, 29007,	// Rune of Luck Lv 2
				505,  4996,	// Rune of Strength Lv 3
				520,  4999,	// Rune of Agility Lv 3
				530, 29011,	// Rune of Vitality Lv 3
				545, 29002,	// Rune of Intellect Lv 3
				555, 29005,	// Rune of Dexterity Lv 3
				565, 29008;	// Rune of Luck Lv 3
			.@card_index = 3;
		}
		else {
			setarray .@enhance[0],
				70,   4994,	// Rune of Strength Lv 1
				72,   4997,	// Rune of Agility Lv 1
				147, 29009,	// Rune of Vitality Lv 1
				177, 29000,	// Rune of Intellect Lv 1
				197, 29003,	// Rune of Dexterity Lv 1
				247, 29006,	// Rune of Luck Lv 1
				297,  4995,	// Rune of Strength Lv 2
				307,  4998,	// Rune of Agility Lv 2
				382, 29010,	// Rune of Vitality Lv 2
				412, 29001,	// Rune of Intellect Lv 2
				437, 29004,	// Rune of Dexterity Lv 2
				487, 29007,	// Rune of Luck Lv 2
				507,  4996,	// Rune of Strength Lv 3
				510,  4999,	// Rune of Agility Lv 3
				540, 29011,	// Rune of Vitality Lv 3
				555, 29002,	// Rune of Intellect Lv 3
				563, 29005,	// Rune of Dexterity Lv 3
				573, 29008;	// Rune of Luck Lv 3
			.@card_index = 2;
		}
	}
	// If there are two enchants already, RNG refine.
	if (.@card[3] > 0) {
		mes "[Dylan]";
		mes "You will already have a max enchants, I offer you a refine RNG with a cost of ^0000FF500,000^000000 zeny and ^0000FF1 Elunium^000000!";
		next;
		mes "Do you want to? If not, I'll retain its original refine!";
		switch(select("Yes:No")) {
			case 1:
				if (Zeny >= 500000 && countitem(985) >= 1) {
					setarray .@refine_arr[0],1,2,3,4,5,6,8,8,9,7,7,7; // array of refine
					set .@refine_rng, rand(12);
					.@refine = .@refine_arr[.@refine_rng];	
					set Zeny, Zeny - 500000;
					delitem 985,1;
					next;
				} else {
					next;
					mes "Hey, don't mess with me. Where's your ^0000FF500,000^000000 zeny and Elunium?!";
					mes "Anyway, I enchanted it nonetheless!";
				}
				break;
			case 2:
				mes "[Dylan]";
				mes "Coolio! Done.";
				break;
		}
	}
	.@size = getarraysize(.@enhance);
	.@r = rand( 1,.@enhance[.@size-2] );
	for ( .@i = 0; .@i < .@size && .@enhance[.@i] < .@r; .@i += 2 )
		continue;
	.@card[ .@card_index ] = .@enhance[.@i+1];
	delitem 6919,20;
	delequip EQI_ARMOR;
	getitem2 .@item_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	close;
}
